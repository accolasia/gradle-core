buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.github.townsfolk:gradle-release:1.2'
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
	}
}


apply plugin: 'release'
apply plugin: "com.bancvue.core-oss"
apply plugin: "com.bancvue.integration-test"
apply plugin: "com.bancvue.maven-publish-ext"
apply plugin: 'com.jfrog.bintray'

repositories {
	jcenter()
}

dependencies {
	compile localGroovy()
	compile gradleApi()
	compile "nl.javadude.gradle.plugins:license-gradle-plugin:0.9.0"

	mainTestCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
		exclude group: 'org.codehaus.groovy'
	}
	mainTestCompile 'cglib:cglib-nodep:2.2.2'
	mainTestCompile 'org.objenesis:objenesis:1.3'
}

check.dependsOn integrationTest

license {
	ext.name = "BancVue, LTD"
	skipExistingHeaders true
}

[licenseMain, licenseFormatMain]*.exclude "licenses/*"
// exclude the test kit source which was copied from a different project (https://github.com/alkemist/gradle-test-kit)
// and has it's own license
[licenseMainTest, licenseFormatMainTest]*.exclude "org/gradle/testkit/**"

class PublishableProject {
	
	String name
	
	String projectUrl
	String scmUrl
	String scmConnection
	String scmDeveloperConnection

	String packaging = "jar"
	String description
	
	String bintrayRepo
	String bintrayUserOrg
	
	
	public static PublishableProject fromGithubUrl(Project project, String projectUrl) {
		
		PublishableProject publishable = new PublishableProject()
		publishable.name = project.ext['artifactId']
		publishable.description = project.description
		publishable.projectUrl =  "https://github.com/${projectUrl}"
		publishable.scmUrl =  "https://github.com/${projectUrl}.git"
		publishable.scmConnection = "scm:git:git@github.com:${projectUrl}.git"
		publishable.scmDeveloperConnection = "scm:git:git@github.com:${projectUrl}.git"
	}
	
}


publishing_ext {
	publication("mainTest")
	publishJavadoc true
	
	bintray {
		fromGithubProject("BancVue/gradle-core")

		syncWithMaven {
			developer {
				id "bancvue_oss"
				name "BancVue Open Source Development"
				email "opensource@bancvue.com"				
			}
		}
	}
	
	pom {
		packaging "jar"
		description project.description
		url "https://github.com/BancVue/gradle-core"
		developers {
			developer {
				id "bancvue_oss"
				name "BancVue Open Source Development"
				email "opensource@bancvue.com"
			}
		}
		scm {
			url "https://github.com/BancVue/gradle-core.git"
			connection "scm:git:git@github.com:BancVue/gradle-core.git"
			developerConnection "scm:git:git@github.com:BancVue/gradle-core.git"
		}
	}
}

bintray {
	user = 'bancvue'
	key = '--changeme--'	

	publications = ['gradleCore', 'gradleCoreTest']
	dryRun = false
	publish = false
	pkg {
		repo = 'maven'
		userOrg = 'bancvue'
		name = project.ext['artifactId']
		desc = project.description
		websiteUrl = 'https://github.com/BancVue/gradle-core'
		issueTrackerUrl = 'https://github.com/BancVue/gradle-core/issues'
		vcsUrl = 'https://github.com/BancVue/gradle-core.git'
		licenses = ['Apache-2.0']
		labels = ['gradle']
		publicDownloadNumbers = true
		version {
			vcsTag = project.version
			attributes = ['gradle-plugin': 'com.bancvue.core-oss:com.bancvue:gradle-core']
		}
	}
}
