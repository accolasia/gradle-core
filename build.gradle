buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.github.townsfolk:gradle-release:1.2'
	}
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'release'
apply plugin: "com.bancvue.core-oss"
apply plugin: "com.bancvue.integration-test"
apply plugin: "com.bancvue.maven-publish-ext"

repositories {
	jcenter()
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
}

dependencies {
	compile localGroovy()
	compile gradleApi()
	compile "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1"

	mainTestCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
		exclude group: 'org.codehaus.groovy'
	}
	mainTestCompile 'cglib:cglib-nodep:3.2.2'
	mainTestCompile 'org.objenesis:objenesis:2.2'
}

check.dependsOn integrationTest

license {
	ext.name = "BancVue, LTD"
	skipExistingHeaders true
	// exclude the test kit source which was copied from a different project (https://github.com/alkemist/gradle-test-kit)
	// and has it's own license
	excludes(["licenses/*", "org/gradle/testkit/**", "**/*.properties"])
}

publishing_ext {
	publication("mainTest")

	publishJavadoc true
	pom {
		packaging "jar"
		description "A collection of gradle plugins meant to codify and streamline project development."
		url "https://github.com/BancVue/gradle-core"
		developers {
			developer {
				id "bancvue_oss"
				name "BancVue Open Source Development"
				email "opensource@bancvue.com"
			}
		}
		scm {
			url "https://github.com/BancVue/gradle-core.git"
			connection "scm:git:git@github.com:BancVue/gradle-core.git"
			developerConnection "scm:git:git@github.com:BancVue/gradle-core.git"
		}
	}
}

bintray {
	user = project.hasProperty("bintray.user") ? project.getProperty("bintray.user") : ""
	key = project.hasProperty("bintray.key") ? project.getProperty("bintray.key") : ""

	publications = ['gradleCore', 'gradleCoreTest']
	pkg {
		repo = 'maven'
		userOrg = 'openmastery'
		name = project.group + ':' + project.ext['artifactId']
		desc = project.description
		websiteUrl = 'https://github.com/openmastery/gradle-core'
		issueTrackerUrl = 'https://github.com/openmastery/gradle-core/issues'
		vcsUrl = 'https://github.com/openmastery/gradle-core.git'
		licenses = ['Apache-2.0']
	}
}
